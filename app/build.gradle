plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'kotlin-parcelize'
//    id 'androidx.navigation.safeargs'
    // Search here by you kotlin version-> https://github.com/google/ksp/releases
    id 'com.google.devtools.ksp' version "1.7.20-1.0.8"
}

android {
    compileSdk compile_sdk

    defaultConfig {
        applicationId "az.zero.azkeepit"
        minSdk min_sdk
        targetSdk target_sdk
        versionCode 1
        versionName "1.0.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            signingConfig signingConfigs.debug
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        beta {
            debuggable true
            signingConfig signingConfigs.debug
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    // For navigation generated classes
    applicationVariants.all { variant ->
        kotlin.sourceSets {
            getByName(variant.name) {
                kotlin.srcDir("build/generated/ksp/${variant.name}/kotlin")
            }
        }
    }

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion compose_version_2
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.9.0'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.5.1'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'

    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-ktx:$room_version"

    // Datastore
    implementation "androidx.datastore:datastore-preferences:$datastore"

    // Java Date-Time desugar
    coreLibraryDesugaring "com.android.tools:desugar_jdk_libs:1.1.5"

    // Gson
    implementation "com.google.code.gson:gson:$gson_version"

    //// Compose
    implementation "androidx.compose.ui:ui:$compose_version_2"
    implementation "androidx.compose.material:material:$compose_version_1"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_version_2"

    implementation "androidx.activity:activity-compose:$compose_activity"
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_version_2"
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_version_2"
    debugImplementation "androidx.compose.ui:ui-test-manifest:$compose_version_2"

    // Compose Utils library
    implementation "com.google.accompanist:accompanist-systemuicontroller:$accompanist"

    // Flow layouts
    implementation "com.google.accompanist:accompanist-flowlayout:$accompanist"

    // Compose Pager
    implementation "com.google.accompanist:accompanist-pager:$accompanist"
    implementation "com.google.accompanist:accompanist-pager-indicators:$accompanist"

    // Simple navigation
    implementation 'io.github.raamcosta.compose-destinations:core:1.7.30-beta'
    ksp 'io.github.raamcosta.compose-destinations:ksp:1.7.30-beta'
    implementation "androidx.navigation:navigation-compose:2.5.3"


    //Dagger - Hilt + Hilt Compose (if not added it will throw error w CreationExtras must have a value by `SAVED_STATE_REGISTRY_OWNER_KEY`)
    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-android-compiler:$hilt_version"
    kapt "androidx.hilt:hilt-compiler:$hilt_compiler"
    implementation "androidx.hilt:hilt-navigation-compose:1.0.0"

    // ViewModel ext for compose
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:2.6.0-alpha03"

    // Ui utils
    implementation "androidx.compose.ui:ui-util:$compose_version_2"

    // Coil
    implementation "io.coil-kt:coil-compose:$compose_coil"

    // More Icons
    implementation "androidx.compose.material:material-icons-extended:$compose_version_1"
}

// Allow references to generated code
kapt {
    correctErrorTypes true
}